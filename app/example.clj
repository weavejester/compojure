;; example
;;
;; This is the basic example application that comes with Compojure for
;; demonstration purposes.

(ns example
  (:use (compojure html
                   http)))

(defn template
  "A function to generate the standard outline of a HTML page."
  [title & body]
  (html
    (doctype :html4)
    [:html
      [:head
        [:title title]]
      [:body
        body]]))

(defn example-form
  "A form with all inbuilt controls."
  []
  (form-to [POST "/form"]
    [:p (label :name "Username:") " "
        (text-field :name "Anonymous")]

    [:p (label :password "Password:") " "
        (password-field :password)]

    [:p (label :sex "Sex:") " "
        (drop-down :sex ["Male" "Female"])]

    [:p (label :profile "Profile:") [:br]
        (text-area {:cols 40 :rows 10} :profile)]

    [:p (label :agree "Have read usage agreement:") " "
        (check-box :agree)]

    [:p (submit-button "New User")
        (reset-button "Reset Form")]))

(defn welcome-page
  "A basic welcome page."
  []
  (template "Greeting"
    [:h1#title "Welcome to Compojure"]
    [:p.info
      "Compojure is an open source web framework for "
      (link-to "http://clojure.org" "Clojure") "."]
    [:p
      "Here is an " (link-to "/form" "example of a form")
      " generated by Compojure."]))

(defservlet example-servlet
  "A Compojure example servlet."
  (GET "/"
    (welcome-page))
  (GET "/form"
    (template "Form" (example-form)))
  (ANY "*"
    (page-not-found)))
